locals {
  global = yamldecode(file(find_in_parent_folders("global.yaml")))
  env    = yamldecode(file("env.yaml"))
}

generate "provider" {
  path      = "providers.tf"
  if_exists = "overwrite"
  contents  = <<EOF

terraform {
  required_version = "~> 1.3"

  backend "s3" {
    bucket       = "${local.global.tfstates.bucket}"
    key          = "${local.env.name}/${path_relative_to_include()}/terraform.tfstate"
    region       = "${local.global.tfstates.region}"
    endpoint     = "${local.global.tfstates.endpoint}"
    access_key   = "${get_env("TFSTATES_ACCESS_KEY_ID")}"
    secret_key   = "${get_env("TFSTATES_SECRET_ACCESS_KEY")}" # checkov:skip=CKV_SECRET_6: this is only an env var the real secret is not commited
    skip_region_validation      = true
    skip_credentials_validation = true
  }

  required_providers {
    local      = "~> 2.3.0"
    tls        = "~> 4.0.4"
    time       = "~> 0.9.1"
    helm       = "~> 2.8.0"
    kubernetes = "~> 2.17.0"
    ovh = {
      source  = "ovh/ovh"
      version = "~> 0.26.0"
    }
    openstack = {
      source = "terraform-provider-openstack/openstack"
      version = "~> 1.49.0"
    }
    pass =  {
      source  = "mecodia/pass"
      version = "~> 3.1.0"
    }
  }
}

provider "ovh" {
  endpoint           = "${local.global.ovh.endpoint}"
  application_key    = "${get_env("OVH_APPLICATION_KEY")}"
  application_secret = "${get_env("OVH_APPLICATION_SECRET")}" # checkov:skip=CKV_SECRET_6: this file is generated by terragrunt and not commited
  consumer_key       = "${get_env("OVH_CONSUMER_KEY")}"
}

# checkov:skip=CKV_OPENSTACK_1: there is actually no credentials just a region
provider "openstack" {
  region      = "${local.env.ovh.public_cloud.region}"
}
EOF
}
